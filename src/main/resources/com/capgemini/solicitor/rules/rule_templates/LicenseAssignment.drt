// Copyright 2019 Capgemini SE.
// SPDX-License-Identifier: Apache-2.0

template header
clientName
engagementName
applicationName
groupId
artifactId
version
declaredLicense
url
normalizedLicenseType
normalizedLicense
normalizedLicenseUrl
comment


package com.capgemini.solicitor.rules;

import com.capgemini.solicitor.model.ModelFactory;
import com.capgemini.solicitor.model.inventory.NormalizedLicense;
import com.capgemini.solicitor.model.inventory.RawLicense;
import com.capgemini.solicitor.model.inventory.ApplicationComponent;
import com.capgemini.solicitor.model.masterdata.Application;
import com.capgemini.solicitor.model.masterdata.Engagement;


template "License Assignment"

rule "License Assignment @{row.rowNumber} - mark RawLicense as done by specialHandling" salience -@{row.rowNumber}
agenda-group "LicenseAssignment" 
    when
    	// make sure that the main conditions given here are the same as in the below rule
        $e : Engagement( 
        	clientName == "@{clientName}", 
        	engagementName == "@{engagementName}",
        	$name :  engagementName )
        $a : Application( 
        	name == "@{applicationName}", 
        	engagement == $e )
        $ac : ApplicationComponent(
        	groupId == "@{groupId}", 
        	artifactId == "@{artifactId}", 
        	version == "@{version}",
        	application == $a )
        $rl : RawLicense( 
            specialHandling == false,						// differs from below rule: it should only fire once per RawLicense
        	declaredLicense == "@{declaredLicense}",
        	$licUrl : licenseUrl,
        	licenseUrl == "@{url}", 
        	applicationComponent == $ac )
    then
        $rl.setSpecialHandling(true);
        update($rl);
       
end

rule "License Assignment @{row.rowNumber} - add given NormalizedLicense" salience -@{row.rowNumber}
agenda-group "LicenseAssignment" 
    when
        $e : Engagement( 
        	clientName == "@{clientName}", 
        	engagementName == "@{engagementName}",
        	$name :  engagementName )
        $a : Application( 
        	name == "@{applicationName}", 
        	engagement == $e )
        $ac : ApplicationComponent(
        	groupId == "@{groupId}", 
        	artifactId == "@{artifactId}", 
        	version == "@{version}",
        	application == $a )
        $rl : RawLicense(  
        	declaredLicense == "@{declaredLicense}",
        	$licUrl : licenseUrl,
        	licenseUrl == "@{url}", 
        	applicationComponent == $ac )
        not( NormalizedLicense(applicationComponent == $ac, normalizedLicense == "@{normalizedLicense}") )  // differs from above
        	
    then
        NormalizedLicense normalizedLicense = ModelFactory.newNormalizedLicense($ac, $rl.getDeclaredLicense(), $licUrl, "@{normalizedLicenseType}" );
        normalizedLicense.setNormalizedLicense("@{normalizedLicense}"); 
        normalizedLicense.setNormalizedLicenseUrl($licUrl);
 		normalizedLicense.setNormalizedLicenseUrl( "@{normalizedLicenseUrl}" ); 
        normalizedLicense.addComment( "@{comment}" );
        insert(normalizedLicense);
       
end

end template
